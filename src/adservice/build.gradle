plugins {
    id 'com.google.protobuf' version '0.8.18'
    id 'idea'
    id 'application'
}

repositories {
    mavenCentral()
    mavenLocal()
}

description = 'Ad Service'
group = "adservice"
version = "0.1.0-SNAPSHOT"

def opentelemetryVersion = "1.9.1"
def grpcVersion = "1.43.2"
def jacksonVersion = "2.13.1"
def protocVersion = "3.19.2"

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

ext {
    speed = project.hasProperty('speed') ? project.getProperty('speed') : false
    offlineCompile = new File("$buildDir/output/lib")
}

dependencies {
    if (speed) {
        implementation fileTree(dir: offlineCompile, include: '*.jar')
    } else {
        compileOnly group: "javax.annotation", name: "javax.annotation-api", version: "1.3.2"

        compile "io.opentelemetry:opentelemetry-api:${opentelemetryVersion}",
                "io.opentelemetry:opentelemetry-sdk:${opentelemetryVersion}",
                "io.opentelemetry:opentelemetry-extension-annotations:${opentelemetryVersion}"

        implementation "com.google.api.grpc:proto-google-common-protos:1.17.0",
                "io.grpc:grpc-protobuf:${grpcVersion}",
                "io.grpc:grpc-stub:${grpcVersion}",
                "io.grpc:grpc-netty:${grpcVersion}",
                "io.grpc:grpc-services:${grpcVersion}",
                "org.apache.logging.log4j:log4j-core:2.17.1"

        runtimeOnly "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}",
                "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}",
                "io.netty:netty-tcnative-boringssl-static:2.0.26.Final"
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
        ofSourceSet('main')
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'msdemo'
            srcDirs 'build/generated/source/proto/main/java/msdemo'
            srcDirs 'build/generated/source/proto/main/grpc/msdemo'
        }
    }
}

startScripts.enabled = false

// This to cache dependencies during Docker image building. First build will take time.
// Subsequent build will be incremental.
task downloadRepos(type: Copy) {
    from configurations.compile
    into offlineCompile
    from configurations.runtime
    into offlineCompile
}

task adService(type: CreateStartScripts) {
    mainClassName = 'msdemo.AdService'
    applicationName = 'AdService'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
    defaultJvmOpts = ["-javaagent:/opt/otel/javaagent.jar"]//, "-Dotel.javaagent.debug=true"]
}

task adServiceClient(type: CreateStartScripts) {
    mainClassName = 'msdemo.AdServiceClient'
    applicationName = 'AdServiceClient'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
    defaultJvmOpts = ["-javaagent:/opt/otel/javaagent.jar"]//, "-Dotel.javaagent.debug=true"]
}

applicationDistribution.into('bin') {
    from(adService)
    from(adServiceClient)
    fileMode = 0755
}
